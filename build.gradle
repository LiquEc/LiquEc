/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */

allprojects {
    group 'io.github.liquec'
}

task wrapper(type: Wrapper) {
    description 'Setup the Gradle wrapper'
    gradleVersion = '4.0.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

apply plugin: 'idea'
apply plugin: 'base'
apply plugin: 'net.researchgate.release'

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencies {
        runtime 'ch.qos.logback:logback-classic:1.2.3'
    }

    checkstyle {
        toolVersion = '8.0'
        configFile = file("$rootProject.projectDir/config/checkstyle/checkstyle.xml")
    }

    [checkstyleMain].each { task ->
        task.logging.setLevel(LogLevel.LIFECYCLE)
    }

    [compileJava].each { task ->
        task.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
    }
}

