/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */

import org.gradle.internal.os.OperatingSystem

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'de.dynamicfiles.projects.gradle.plugins:javafx-gradle-plugin:8.8.2'
    }
}

apply plugin: 'javafx-gradle-plugin'

ext {
    publishedVersion = project.version
    bundleMainClass = 'io.github.liquec.gui.distribution.PackagedLiquEc'
    operatingSystem = OperatingSystem.current()
    if (operatingSystem.isMacOsX()) {
        packageType = 'dmg'
        bundleMainClass = 'io.github.liquec.osx.distribution.OsxPackagedLiquEc'
        appIcon = file('icons/mac/LiquEc.icns')
        sessionIcon = file('icons/mac/LiquEcSession.icns')
    } else if (operatingSystem.isLinux()) {
        packageType = 'deb'
        appIcon = file('icons/App1024.png')
        sessionIcon = file('icons/Doc1024.png')
    } else if (operatingSystem.isWindows()) {
        packageType = 'exe'
        appIcon = file('icons/windows/LiquEc.ico')
        sessionIcon = file('icons/windows/LiquEcSession.ico')
    }
    if (publishedVersion.endsWith('-SNAPSHOT')) {
        publishedVersion = '1.0.0'
    }
}

dependencies {
    compile project(':gui')
    if (OperatingSystem.current().isMacOsX()) {
        compile project(':osx')
    }
}

if (operatingSystem.isMacOsX()) {
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
                srcDir 'src/osx/java'
            }
        }
    }
}

task recreateIconMacApp(type: Exec) {
    description 'Regenerate the Mac application icon'
    commandLine 'sh', file('bin/mac-icon.sh'), file('icons/App1024.png'), file('icons/mac'), 'LiquEc'
}

task recreateIconMacSession(type: Exec) {
    description 'Regenerate the Mac session file icon'
    commandLine 'sh', file('bin/mac-icon.sh'), file('icons/Doc1024.png'), file('icons/mac'), 'LiquEcSession'
}

task recreateIconWindowsApp(type: Exec) {
    description 'Regenerate the Windows application icon'
    commandLine 'sh', file('bin/windows-icon.sh'), file('icons/App1024.png'), file('icons/windows'), 'LiquEc'
}

task recreateIconWindowsSession(type: Exec) {
    description 'Regenerate the Windows session file icon'
    commandLine 'sh', file('bin/windows-icon.sh'), file('icons/Doc1024.png'), file('icons/windows'), 'LiquEcSession'
}

task recreateIcons(dependsOn: [recreateIconMacApp, recreateIconMacSession, recreateIconWindowsApp, recreateIconWindowsSession]) {
    description 'Regenerate all the application bundle icons'
}

jfx {
    mainClass = bundleMainClass
    vendor = 'liquec.github.io'
    appName = 'LiquEc'
    nativeReleaseVersion = publishedVersion
    bundler = packageType
    bundleArguments = [
        icon: appIcon,
        runtime: null,
        installdirChooser: true
    ]
    fileAssociations = [
        [
            description: 'LiquEc session',
            extensions: 'liquec',
            contentType: 'application/x-vnd.LiquEcSession',
            icon: sessionIcon
        ]
    ]
}

task checkBundle {
    doLast {
        String bundle = "LiquEc-${publishedVersion}.${packageType}"
        FileTree tree = fileTree('build/jfx/native') {
            include {
                it.file.name.equalsIgnoreCase(bundle)
            }
        }
        if (tree.isEmpty()) {
            throw new GradleException("Unable to create bundle file '$bundle'")
        } else {
            tree.each {File f ->
                println "Created bundle '$f'"
            }
        }
    }
}
